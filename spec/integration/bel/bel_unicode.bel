##################################################################################
# Document Properties Section

SET DOCUMENT Name = "BEL Framework Large Corpus Document"
SET DOCUMENT Description = "Approximately 61,000 statements."
SET DOCUMENT Version = 20150611
SET DOCUMENT Copyright = "Copyright (c) 2011-2015, Selventa. All rights reserved."
SET DOCUMENT Authors = Selventa
SET DOCUMENT Licenses = "Creative Commons Attribution-Non-Commercial-ShareAlike 3.0 Unported License"
SET DOCUMENT ContactInfo = "support@belframework.org"

##################################################################################
# Definitions Section

DEFINE NAMESPACE CHEBI AS URL "http://resource.belframework.org/belframework/20150611/namespace/chebi.belns"
DEFINE NAMESPACE EGID AS URL "http://resource.belframework.org/belframework/20150611/namespace/entrez-gene-ids.belns"
DEFINE NAMESPACE GOBP AS URL "http://resource.belframework.org/belframework/20150611/namespace/go-biological-process.belns"
DEFINE NAMESPACE GOCCID AS URL "http://resource.belframework.org/belframework/20150611/namespace/go-cellular-component-ids.belns"
DEFINE NAMESPACE HGNC AS URL "http://resource.belframework.org/belframework/20150611/namespace/hgnc-human-genes.belns"
DEFINE NAMESPACE MESHCS AS URL "http://resource.belframework.org/belframework/20150611/namespace/mesh-cellular-structures.belns"
DEFINE NAMESPACE MESHD AS URL "http://resource.belframework.org/belframework/20150611/namespace/mesh-diseases.belns"
DEFINE NAMESPACE MESHPP AS URL "http://resource.belframework.org/belframework/20150611/namespace/mesh-processes.belns"
DEFINE NAMESPACE MGI AS URL "http://resource.belframework.org/belframework/20150611/namespace/mgi-mouse-genes.belns"
DEFINE NAMESPACE RGD AS URL "http://resource.belframework.org/belframework/20150611/namespace/rgd-rat-genes.belns"
DEFINE NAMESPACE SCHEM AS URL "http://resource.belframework.org/belframework/20150611/namespace/selventa-legacy-chemicals.belns"
DEFINE NAMESPACE SCOMP AS URL "http://resource.belframework.org/belframework/20150611/namespace/selventa-named-complexes.belns"
DEFINE NAMESPACE SDIS AS URL "http://resource.belframework.org/belframework/20150611/namespace/selventa-legacy-diseases.belns"
DEFINE NAMESPACE SFAM AS URL "http://resource.belframework.org/belframework/20150611/namespace/selventa-protein-families.belns"

DEFINE ANNOTATION Anatomy AS URL "http://resource.belframework.org/belframework/20150611/annotation/anatomy.belanno"
DEFINE ANNOTATION Cell AS URL "http://resource.belframework.org/belframework/20150611/annotation/cell.belanno"
DEFINE ANNOTATION CellLine AS URL "http://resource.belframework.org/belframework/20150611/annotation/cell-line.belanno"
DEFINE ANNOTATION CellStructure AS URL "http://resource.belframework.org/belframework/20150611/annotation/cell-structure.belanno"
DEFINE ANNOTATION Disease AS URL "http://resource.belframework.org/belframework/20150611/annotation/disease.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "http://resource.belframework.org/belframework/20150611/annotation/mesh-anatomy.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "http://resource.belframework.org/belframework/20150611/annotation/mesh-diseases.belanno"
DEFINE ANNOTATION Species AS URL "http://resource.belframework.org/belframework/20150611/annotation/species-taxonomy-id.belanno"

##################################################################################
# Statements Section

SET STATEMENT_GROUP = "Group 1"

SET Cell = "muscle cell"
SET Anatomy = heart
SET Support = "By 15min, IL-1b (10 ng/ml) stimulated the appearance of a second band of reduced mobility [Fig. 4(a), upper panel] consistent with the appearance of a more highly phosphorylated form (Clerk & Sugden, 1997)."
SET Species = 10116
SET Citation = {"PubMed","J Mol Cell Cardiol 1999 Dec 31(12) 2087-99","10640438","","",""}

p(RGD:Il1b) increases tscript(p(RGD:Jun))

SET Support = "Figure 3 Activation of ERKs by IL-1b and TNFa in ventricular myocytes. "

p(RGD:Il1b) increases kin(p(RGD:Mapk3))
p(RGD:Tnf) increases kin(p(RGD:Mapk1))

SET Support = "IL-1b (100 ng/ml) stimulated a rapid increase in the activities of the 46 kDa (p46) and 54 kDa (p54) JNKs [Fig. 1(a)]....TNFa (10 ng/ml) also stimulated JNK activities [Fig. 1(b)]."

p(RGD:Il1b) increases kin(p(RGD:Mapk8))
p(RGD:Tnf) increases kin(p(RGD:Mapk9))

SET Support = "Stimulation of p46 and p54 JNKs by IL-1b (100 ng/ml, 10 min) were 84±3% and 55±7% of the response to sorbitol, respectively (means±S.E.M. for three independent observations)."

a(SCHEM:Sorbitol) increases kin(p(RGD:Mapk8))

SET Support = "TNFa (10 ng/ml) also increased ATF2 phosphorylation, with maximal stimulation within 5–15 min [Fig. 6(b)]."

p(RGD:Tnf) increases tscript(p(RGD:Atf2))

UNSET STATEMENT_GROUP

SET STATEMENT_GROUP = "Group 11676"

SET Support = "Previously, we observed that alloxan-induced in vitro cytotoxicity and apoptosis in an insulin secreting rat insulinoma, RIN, cells was prevented by prior exposure to prostaglandin (PG) E(1), PGE(2), PGI(2), PGF(1)(alpha), and PGF(3)(alpha) (P<0.05 compared to alloxan)"
SET Species = 10116
SET Citation = {"PubMed","Prostaglandins Leukot Essent Fatty Acids 2006 Jan 74(1) 39-60","16297610","","",""}

a(SCHEM:"Prostaglandin E2 methyl ester") decreases path(SDIS:Diabetes)

SET CellLine = "RIN-m5F cell"

a(SCHEM:"Prostaglandin E2 methyl ester") decreases bp(MESHPP:Apoptosis)
a(SCHEM:"Prostaglandin I2 methyl ester") decreases bp(MESHPP:Apoptosis)
a(SCHEM:"Prostaglandin F1") decreases bp(MESHPP:Apoptosis)


a(CHEBI:"prostaglandin F3α") decreases path(SDIS:Diabetes)

SET CellLine = "RIN-m5F cell"

a(CHEBI:"prostaglandin F3α") decreases bp(MESHPP:Apoptosis)

UNSET STATEMENT_GROUP

